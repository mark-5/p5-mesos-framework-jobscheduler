#!/usr/bin/env perl
use strict;
use warnings;
use FindBin::libs;
use Getopt::Long::Descriptive;
use Mesos::JobScheduler;
use Mesos::SchedulerDriver;
use Proc::Daemon;

# PODNAME: start-server
my ($opt, $usage) = describe_options(
    "$0 %o",
    ['daemonize|d', 'run the server in the background'],
    ['listen|l=s',  'the address to listen to http requests on'],
    ['master|m=s',  'the mesos master to use', {required => 1}],
    ['pidfile|p=s', 'the path to the pid file, if running with --daemonize', {default => '/var/run/mesos-jobscheduler.pid'}],
    ['zk|z=s',      'the address of the zookeeper server to use', {required => 1}],
);
my ($host, $port) = split ':', $opt->{listen} // '';
$host ||= '0.0.0.0';
$port ||= 8080;

my $daemon = Proc::Daemon->new(
    pid_file => $opt->{pidfile},
);
$daemon->Init if $opt->{daemonize};

my $config = {
    api => {
        http => {
            host => $host,
            port => $port,
        },
    },
    zookeeper => {
        hosts => $opt->{zk} // 'localhost:2181',
    },
};

my $app = Mesos::JobScheduler->new(
    config => $config,
);
my $api = $app->resolve(service => 'api');

my $driver = Mesos::SchedulerDriver->new(
    dispatcher => 'AnyEvent',
    framework  => {
        user => '',
        name => 'Mesos::JobScheduler Framework',
    },
    master    => $opt->{master},
    scheduler => $api->framework,
);

$SIG{INT} = sub { exit 0 };
$driver->run;
